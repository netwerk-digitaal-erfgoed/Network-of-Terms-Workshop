PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX text: <http://jena.apache.org/text#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX :        <https://d-nb.info/gnd/>
PREFIX gndo:    <https://d-nb.info/standards/elementset/gnd#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

CONSTRUCT {
    ?person a skos:Concept ;
       skos:prefLabel ?preferredNameForThePerson ;
       skos:altLabel ?variantNameForThePerson ;
       skos:scopeNote ?scopeNote;
       rdfs:seeAlso ?sameAs .
}
where {
  ?person text:query "Jansen" .
  ?person a gndo:DifferentiatedPerson ;
          gndo:preferredNameForThePerson ?preferredNameForThePerson .
  BIND("" as ?s1)
  OPTIONAL { ?person gndo:variantNameForThePerson ?variantNameForThePerson }
  OPTIONAL { ?person gndo:dateOfBirth ?dateOfBirth  }
  OPTIONAL { ?person gndo:placeOfBirth/gndo:preferredNameForThePlaceOrGeographicName ?placeOfBirth }
  OPTIONAL { ?person gndo:dateOfDeath ?dateOfDeath }
  OPTIONAL { ?person gndo:placeOfDeath/gndo:preferredNameForThePlaceOrGeographicName ?placeOfDeath }
  OPTIONAL { ?person owl:sameAs ?sameAs }
  OPTIONAL { ?person gndo:professionOrOccupation/gndo:preferredNameForTheSubjectHeading ?professionOrOccupation }
  BIND(IF(BOUND(?dateOfBirth),CONCAT(?s1,"born in ", STR(?dateOfBirth), " "),?s1) as ?s2)
  BIND(IF(BOUND(?placeOfBirth),CONCAT(?s2," (",?placeOfBirth,") "),?s2) as ?s3)
  BIND(IF(BOUND(?dateOfDeath),CONCAT(?s3," deceased in ", STR(?dateOfDeath)),?s3) as ?s4)
  BIND(IF(BOUND(?placeOfDeath),CONCAT(?s4," (",?placeOfDeath,") "),?s4) as ?scopeNote)  
  #BIND(IF(BOUND(?professionOrOccupation),CONCAT(?s5," ", ?professionOrOccupation),?s5) as ?scopeNote)
} limit 100