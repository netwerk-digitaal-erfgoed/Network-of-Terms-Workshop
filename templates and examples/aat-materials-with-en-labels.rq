PREFIX aat: <http://vocab.getty.edu/aat/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX gvp: <http://vocab.getty.edu/ontology#>
PREFIX luc: <http://www.ontotext.com/owlim/lucene#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX skosxl: <http://www.w3.org/2008/05/skos-xl#>
PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
    ?uri a skos:Concept ;
        skos:prefLabel ?prefLabel ;
        skos:altLabel ?altLabel ;
        skos:scopeNote ?scopeNote ;
        rdfs:seeAlso ?rdfs_seeAlso ;
        skos:broader ?broader_uri ;
        skos:narrower ?narrower_uri .
    ?broader_uri skos:prefLabel ?broader_prefLabel .
    ?narrower_uri skos:prefLabel ?narrower_prefLabel .
}
WHERE {

  # !this is where magic happens!
  # - the ?query parameters is being replaced on runtime by the actual search string
  # - this Getty sparql endpoint uses a Lucene index for full text search 
  # - uris are selected that match the search string on prefLabels, altLabels or the subject ID (default)
  # - see more information in http://vocab.getty.edu/doc/#Full_Text_Search
  ?uri luc:term ?query .

  # restrict results to the AAT datasets (often sparql endpoints contain multiple datasets)
  ?uri void:inDataset <http://vocab.getty.edu/dataset/aat>  .
    
  # allow any class that is a subclass of gvn:Subject
  # but ignore the duplicate gvn:Subject itself
  ?uri a ?type .
  ?type rdfs:subClassOf gvp:Subject .
  FILTER (?type != gvp:Subject) .

  # limit results to the "materials" hierarchy (300010357)
  ?uri gvp:broaderPreferred+ <http://vocab.getty.edu/aat/300010357> .

  # read the prefLabel string in the English language
  ?uri skosxl:prefLabel ?prefLabel_uri .
  ?prefLabel_uri skosxl:literalForm ?prefLabel .
  ?prefLabel_uri dcterms:language aat:300388277 . # English (language)

  # gather more information in the same way, if available
  OPTIONAL {
    ?uri skosxl:altLabel ?altLabel_uri .
    ?altLabel_uri skosxl:literalForm ?altLabel .
    ?altLabel_uri dcterms:language aat:300388277 . # English (language)
        
  }
  OPTIONAL {
    ?uri skos:scopeNote ?scopeNote_uri .
    ?scopeNote_uri rdf:value ?scopeNote .
    ?scopeNote_uri dcterms:language aat:300388277 . # English (language)
  }
  OPTIONAL {
    ?uri rdfs:seeAlso ?rdfs_seeAlso
  }
  OPTIONAL {
    ?uri gvp:broaderPreferred ?broader_uri .
    ?broader_uri skosxl:prefLabel ?broader_uri_skosxl .
    ?broader_uri_skosxl skosxl:literalForm ?broader_prefLabel .
    ?broader_uri_skosxl dcterms:language aat:300388277 . # English (language)

  }
  OPTIONAL {
    ?uri skos:narrower ?narrower_uri .
    ?narrower_uri skosxl:prefLabel ?narrower_uri_skosxl .
    ?narrower_uri_skosxl skosxl:literalForm ?narrower_prefLabel .
    ?narrower_uri_skosxl dcterms:language aat:300388277 . # English (language)
  }
}
LIMIT 1000
